
/*
 * Copyright (c) 2020 NetFoundry. All rights reserved.
 */

plugins {
    id('com.android.application')
    id('kotlin-android')
}

version = parent.version

def vc = versionCode

android {
    namespace = "org.openziti.mobile"
    signingConfigs {
        release {
            keyAlias 'ziti1'
            keyPassword System.getenv('RELEASE_KEYSTORE_PASSWORD')
            storeFile file(System.getenv('RELEASE_KEYSTORE') ?: "not found")
            storePassword System.getenv('RELEASE_KEYSTORE_PASSWORD')
        }
    }
    compileSdk = 33
    ndkVersion "22.1.7171670"

    defaultConfig {
        applicationId "org.openziti.mobile"
        minSdkVersion 26
        targetSdkVersion 33
        versionCode vc
        versionName "${project.version}"

        buildConfigField "String", "GIT_COMMIT", "\"${gitHash}\""
        buildConfigField "String", "GIT_BRANCH", "\"${gitBranch}\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
    }
}

repositories {
    mavenLocal()
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation("org.openziti:ziti-android:${versions.ziti}@aar") {
        transitive = true
    }

    implementation("com.noveogroup.android:android-logger:1.3.6")

    implementation("org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:${versions.coroutines}")


    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    implementation 'com.journeyapps:zxing-android-embedded:4.3.0'

    implementation('org.pcap4j:pcap4j-core:1.8.2'){
        transitive = false
        exclude(group: 'net.java.dev.jna', module: 'jna')
    }
    implementation('org.pcap4j:pcap4j-packetfactory-static:1.8.2') {
        exclude(group: 'net.java.dev.jna', module: 'jna')
    }

    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:${versions.androidxLifecycle}"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:${versions.androidxLifecycle}"

    testImplementation 'org.hamcrest:hamcrest-core:2.2'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}
